Процедура ПередПодключением(КлиентКлюч, Параметры) Экспорт 
	ВебСокетыСервер.ЗаписатьВЛог("ПередПодключением",КлиентКлюч,Параметры);	
КонецПроцедуры

Процедура ПриОткрытииСоединения(СоединениеКлюч,Параметры) Экспорт
	ВебСокетыСервер.ЗаписатьВЛог("ПриОткрытииСоединения",СоединениеКлюч,Параметры);
КонецПроцедуры

Процедура ПриПолученииСообщения(СоединениеКлюч, Параметры) Экспорт 
	ВебСокетыСервер.ЗаписатьВЛог("ПриПолученииСообщения",СоединениеКлюч,Параметры);
КонецПроцедуры

Процедура ПриОшибке(СоединениеКлюч, Параметры) Экспорт  
	ВебСокетыСервер.ЗаписатьВЛог("ПриОшибке",СоединениеКлюч,Параметры);
КонецПроцедуры    

Процедура ПриЗакрытииСоединения(СоединениеКлюч, Параметры) Экспорт 	
	ВебСокетыСервер.ЗаписатьВЛог("ПриЗакрытииСоединения",СоединениеКлюч,Параметры);
КонецПроцедуры

Процедура ОбработчикОткрытия(СоединениеКлюч,Параметры) Экспорт 
	ВебСокетыСервер.ЗаписатьВЛог("ОбработчикОткрытия",СоединениеКлюч,Параметры);	
КонецПроцедуры   

Процедура ОбработчикПолученияСообщения(СоединениеКлюч, Параметры) Экспорт
	ВебСокетыСервер.ЗаписатьВЛог("ОбработчикПолученияСообщения",СоединениеКлюч,Параметры);
КонецПроцедуры    

Процедура ОбработчикОшибки(СоединениеКлюч, Параметры) Экспорт  
	ВебСокетыСервер.ЗаписатьВЛог("ОбработчикОшибки",СоединениеКлюч,Параметры);
КонецПроцедуры 

Процедура ОбработчикЗакрытия(СоединениеКлюч, Параметры) Экспорт  
	ВебСокетыСервер.ЗаписатьВЛог("ОбработчикЗакрытия",СоединениеКлюч,Параметры);	
КонецПроцедуры 
  
Процедура ОбработчикОтправитьСообщение(СоединениеКлюч, Параметры) Экспорт  
	ВебСокетыСервер.ЗаписатьВЛог("ОбработчикОтправитьСообщение",СоединениеКлюч,Параметры);	
КонецПроцедуры   
  
Процедура ЗаписатьВЛог(Обработчик,Ключ,Параметры=Неопределено) Экспорт 
	Дата = ТекущаяДатаСеанса();
	Если Не ЗначениеЗаполнено(Параметры) Тогда 
		Параметры = Новый Структура;
	КонецЕсли;	
	
	
	БылаОшибка = Ложь;
	Попытка	
		               
		НаборЗаписей = РегистрыСведений.СобытияВебСокеты.СоздатьНаборЗаписей();  			
		НаборЗаписей.Отбор.Дата.Установить(Дата);
		НаборЗаписей.Отбор.Обработчик.Установить(Обработчик);
		НаборЗаписей.Отбор.Ключ.Установить(Ключ);
		НаборЗаписей.Прочитать();	

		Если НаборЗаписей.Количество() = 0 Тогда
			НоваяЗаписьРегистра = НаборЗаписей.Добавить();
			
			НоваяЗаписьРегистра.Дата 		= Дата;
			НоваяЗаписьРегистра.Обработчик 	= Обработчик;
			НоваяЗаписьРегистра.Ключ 		= Ключ;	
		Иначе
			НоваяЗаписьРегистра = НаборЗаписей[0];
		КонецЕсли;
				
       	Для Каждого текПараметр Из Параметры Цикл 
				
			НоваяЗаписьРегистра[текПараметр.Ключ] = текПараметр.Значение;	
			
		КонецЦикла;
		
		
		НаборЗаписей.Записать(); 
					
	Исключение	
		
		БылаОшибка = Истина;
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			
	КонецПопытки;

	Если БылаОшибка Тогда 
		ЗаписьЖурналаРегистрации("Ошибка",УровеньЖурналаРегистрации.Ошибка,,,ТекстОшибки);
	КонецЕсли;	
	
КонецПроцедуры


#Область СериализацияДесериализацияДанных 

// Возвращает чаще всего используемые ПараметрыЗаписиJSON (JSONWriterSettings)
// 
// Возвращаемое значение:
//  Результат - Структура - Стандартное заполнение
//
Функция СтандартныеПараметрыJSON() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ПереносСтрок",					ПереносСтрокJSON.Авто);
	Результат.Вставить("СимволОтступа",					" ");
	Результат.Вставить("ИспользоватьДвойныеКавычки",	Истина);
	Результат.Вставить("ЭкранированиеСимволов",			ЭкранированиеСимволовJSON.Нет);
	Результат.Вставить("ЭкранироватьАмперсанд",			Ложь);
	Результат.Вставить("ЭкранироватьОдинарныеКавычки",	Ложь);
	Результат.Вставить("ЭкранироватьРазделителиСтрок",	Ложь);
	Результат.Вставить("ЭкранироватьУгловыеСкобки",		Ложь);
	Результат.Вставить("ЭкранироватьСлеш",				Ложь);	
	
	Возврат Результат;		
	
КонецФункции

// Преобразует данные в формат JSON
//
// Параметры:
//  ВходящиеПараметры	 - Структура - Параметры формирование JSON
//  ВходныеДанные		 - ЛюбоеЗначение - Данные которые нужно перевести в JSON
// 
// Возвращаемое значение:
//  Результат - Структура
//  	Отработал - Булево - Выполнено или нет
//  	ТекстОшибки - Строка - Текст ошибки если функция отработала с ошибкой
//  	Результат - Строка - JSON
//
Функция ЗаписатьДанныеВJSON(Знач ВходящиеПараметры = Неопределено, Знач ВходныеДанные = "") Экспорт
	
	Результат = Новый Структура("Результат, Отработал, ТекстОшибки", "", Истина, "");
	
	Если ВходящиеПараметры = Неопределено Тогда 
		ВходящиеПараметры = СтандартныеПараметрыJSON(); 	
	КонецЕсли;	
	
	ПараметрыJSON	= Новый ПараметрыЗаписиJSON(ВходящиеПараметры.ПереносСтрок,
												ВходящиеПараметры.СимволОтступа,
												ВходящиеПараметры.ИспользоватьДвойныеКавычки,
												ВходящиеПараметры.ЭкранированиеСимволов,
												ВходящиеПараметры.ЭкранироватьУгловыеСкобки,
												ВходящиеПараметры.ЭкранироватьРазделителиСтрок,
												ВходящиеПараметры.ЭкранироватьАмперсанд,
												ВходящиеПараметры.ЭкранироватьОдинарныеКавычки,
												ВходящиеПараметры.ЭкранироватьСлеш);
	
	
	Попытка
		ЗаписьJSON						= Новый ЗаписьJSON;
		ЗаписьJSON.ПроверятьСтруктуру 	= Истина;
		ЗаписьJSON.УстановитьСтроку(ПараметрыJSON);	
		ЗаписатьJSON(ЗаписьJSON, ВходныеДанные,,"ФункцияПреобразованияЗаписи",ВебСокетыСервер);
		Результат.Вставить("Результат",	ЗаписьJSON.Закрыть());
	Исключение
		Результат.Отработал 			= Ложь;
		Результат.ТекстОшибки 			= ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции	

Функция ФункцияПреобразованияЗаписи(Свойство, Значение, ДополнительныеПараметры, Отказ) Экспорт
    Если ТипЗнч(Значение) = Тип("СписокЗначений") Тогда  
	    МассивЗначений = Значение.ВыгрузитьЗначения();
		Значение = МассивЗначений;
	КонецЕсли;	
КонецФункции

// Десериализует строку JSON в формат данных 1С
//
// Параметры:
//  СтрокаJSON - Строка - JSON
// 
// Возвращаемое значение:
//  Результат - Структура
//  	Отработал - Булево - Выполнено или нет
//  	ТекстОшибки - Строка - Текст ошибки если функция отработала с ошибкой
//  	Результат - ЛюбоеЗначение - Данные в формате 1С
//
Функция ЧтениеДанныхИзJSON(Знач СтрокаJSON) Экспорт
	
	Результат = Новый Структура("Отработал, ТекстОшибки", Истина, "");
						
	Попытка
		ЧтениеJSON 				= Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(СтрокаJSON);
		Результат.Вставить("Результат", ПрочитатьJSON(ЧтениеJSON));
	Исключение
		Результат.Отработал 	= Ложь;
		Результат.ТекстОшибки 	= ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

// Десериализует поток JSON в формат данных 1С
//
// Параметры:
//  ПотокJSON	 - Поток -
// 
// Возвращаемое значение:
//  Результат - Структура
// 		Отработал - Булево - Выполнено или нет
//  	ТекстОшибки - Строка - Текст ошибки если функция отработала с ошибкой
//  	Результат - ЛюбоеЗначение - Данные в формате 1С
//
Функция ЧтениеПотокаИзJSON(Знач ПотокJSON) Экспорт
	
	Результат = Новый Структура("Отработал, ТекстОшибки", Истина, "");
					
	Попытка
		ЧтениеJSON 				= Новый ЧтениеJSON;
		ЧтениеJSON.ОткрытьПоток(ПотокJSON);
		Результат.Вставить("Результат", ПрочитатьJSON(ЧтениеJSON));
	Исключение
		Результат.Отработал 	= Ложь;
		Результат.ТекстОшибки 	= ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти


