
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Для Каждого текВебсокет Из Метаданные.WebSocketКлиенты Цикл 
		
		Элементы.ИмяВебсокета.СписокВыбора.Добавить(текВебсокет.Имя,текВебсокет.Имя);
		
	КонецЦикла;	
	
	Если Элементы.ИмяВебсокета.СписокВыбора.Количество() > 0 Тогда 
		ИмяВебсокета = Элементы.ИмяВебсокета.СписокВыбора[0];
	КонецЕсли;	
	
КонецПроцедуры


&НаКлиенте
Процедура СоздатьХранилищеЗначений(Команда)
	
	Результат = СоздатьWSХранилище(ИмяВебсокета, КлючВебсокета);
	
	Если Не Результат Тогда 
		
		Сообщить("нужно проверить имя объекта метаданных или ключ не уникален");	
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьЗначения(Команда)
	
	ЗаписатьДанныеWSХранилище(КлючВебсокета); 
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочитатьДанные(Команда)
	
	ПрочитатьДанныеWSХранилище(КлючВебсокета);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьСтруктуруВХЗ(Команда)
	
	ЗаписываемаяСтруктура = Новый Структура("Тест,Тест2", 11,"Вася");
	НовыйМассив = Новый Массив;
	НовыйМассив.Добавить(1);
	НовыйМассив.Добавить("текст");
	ЗаписываемаяСтруктура.Вставить("МассивДоКучи",НовыйМассив);
	
	ЗаписатьДанныеВЗаголовокПоКлючу(КлючВебсокета, КлючХранилища, ЗаписываемаяСтруктура);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочитатьДанныеХЗ(Команда)
	Результат = ПрочитатьДанныеЗаголовокаПоКлючу(КлючВебсокета, КлючХранилища);
КонецПроцедуры



// Предопределяем WebSocket-клиент
//
// Параметры:
//  ИмяОбъектаМетаданныхWS	 - Строка - Имя объекта метаданных WebSocket-клиенты
//  КлючWS					 - Строка - Уникальный ключ WebSocket-клиента 
// 
// Возвращаемое значение:
//  Булево - Если Истина WebSocket-клиент предопределен, Ложь нужно проверить имя объекта метаданных или ключ не уникален
//
&НаСервере
Функция СоздатьWSХранилище(Знач ИмяОбъектаМетаданныхWS, Знач КлючWS) Экспорт
	
	Результат = Ложь;
	
	WSОбъектаМетаданных = Метаданные.WebSocketКлиенты.Найти(ИмяОбъектаМетаданныхWS);	
	Если WSОбъектаМетаданных <> Неопределено Тогда 
		WSКлиент = WebSocketКлиенты.НайтиПоКлючу(КлючWS);
		
		Если WSКлиент = Неопределено Тогда  
			
			WSКлиент = WebSocketКлиенты.СоздатьКлиента(WSОбъектаМетаданных, КлючWS); 	
			WSКлиент.Записать();
			
			Результат = Истина;	
			
		КонецЕсли;		
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Добавляем данные в предопределенный WebSocket-клиент
//
// Параметры:
//  КлючWS	 - Строка - Уникальный ключ WebSocket-клиента 
//
&НаСервере
Процедура ЗаписатьДанныеWSХранилище(знач КлючWS) Экспорт
	
	ВебсокетКлиент = WebSocketКлиенты.НайтиПоКлючу(КлючWS); 
	Если ВебсокетКлиент <> Неопределено Тогда 
		Для Каждого текЭлемент из Список Цикл 
			ВебсокетКлиент.ПараметрыСоединения.Заголовки.Вставить(текЭлемент.Ключ,текЭлемент.Значение);		
		КонецЦикла;		
		ВебсокетКлиент.Записать();
	КонецЕсли;     
	
КонецПроцедуры

// Прочитать данные из предопределенного WebSocket-клиент
//
// Параметры:
//  КлючWS	- Строка - Уникальный ключ WebSocket-клиента
//
&НаСервере
Процедура ПрочитатьДанныеWSХранилище(знач КлючWS) Экспорт
	
	Если Список.Количество() > 0 Тогда 
		Список.Очистить();
	КонецЕсли;	
	
	ВебсокетКлиент = WebSocketКлиенты.НайтиПоКлючу(КлючWS); 
	Если ВебсокетКлиент <> Неопределено Тогда 
		Для Каждого текЭлемент из ВебсокетКлиент.ПараметрыСоединения.Заголовки Цикл 
			новаяСтрока = Список.Добавить();
			
			новаяСтрока.Ключ = текЭлемент.Ключ;
			новаяСтрока.Значение = текЭлемент.Значение;	
		
		КонецЦикла;
		
	КонецЕсли;

КонецПроцедуры


// Записываем данные в заголовок предопределенного WebSocket-клиента по ключу
//
// Параметры:
//  КлючWS		 - Строка - Уникальный ключ WebSocket-клиента 
//  КлючЗаголовка - Строка - Имя заголовка
//  Значение	 - Произвольный	 - Записываемые данные
//
&НаСервере
Процедура ЗаписатьДанныеВЗаголовокПоКлючу(Знач КлючWS, Знач КлючЗаголовка, Знач Значение) Экспорт
	
	ВебсокетКлиент = WebSocketКлиенты.НайтиПоКлючу(КлючWS); 
	Если ВебсокетКлиент <> Неопределено Тогда 
		
		ЗначениеЗаголовка = Новый ХранилищеЗначения(Значение);
		ВебсокетКлиент.ПараметрыСоединения.Заголовки.Вставить(КлючЗаголовка, ЗначениеЗаголовка);		
		ВебсокетКлиент.Записать();
		
	КонецЕсли;
		
КонецПроцедуры

// Прочитать данные заголовка по ключу
//
// Параметры:
//  КлючWS		 - Строка - Уникальный ключ WebSocket-клиента 
//  КлючЗаоловка - Строка - Имя заголовка
// 
// Возвращаемое значение:
//  Произвольный - значение заголовка
//
&НаСервере
Функция ПрочитатьДанныеЗаголовокаПоКлючу(Знач КлючWS, Знач КлючЗаголовка) Экспорт
	
	Результат = Неопределено;
	
	ВебсокетКлиент = WebSocketКлиенты.НайтиПоКлючу(КлючWS); 
	Если ВебсокетКлиент <> Неопределено Тогда 
		
		ТекущийЗаколовок = ВебсокетКлиент.ПараметрыСоединения.Заголовки[КлючЗаголовка];		
		Если ТекущийЗаколовок <> Неопределено Тогда 
			Если ТипЗнч(ТекущийЗаколовок) = Тип("ХранилищеЗначения") Тогда 
				Результат = ТекущийЗаколовок.Получить();
			Иначе
				Результат = ТекущийЗаколовок;
			КонецЕсли; 
		КонецЕсли;	
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

