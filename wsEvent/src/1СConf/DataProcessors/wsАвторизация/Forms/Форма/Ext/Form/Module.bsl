
&НаКлиенте
Процедура ЛогинПарольРасширение(Команда)
	ЛогинПарольРасширениеНаСервере();
КонецПроцедуры

&НаСервере
Процедура ЛогинПарольРасширениеНаСервере()

	МетаКлиент = Метаданные.WebSocketКлиенты.Найти("WS_КлиентРасширение");	
	ВебсокетКлиент = WebSocketКлиенты.НайтиПредопределенный(МетаКлиент);
	
	Если ВебсокетКлиент = Неопределено Тогда  
		
		// Создаем клиента с уникальным ключем
		НовыйКлюч = КлючСоединения();
		ВебсокетКлиент = WebSocketКлиенты.СоздатьКлиента(МетаКлиент, НовыйКлюч); 
		
		ВебсокетКлиент.Записать();
		
	КонецЕсли;	
	
	// Если соединение закрыто, тогда вношу наш заголовок 
	// для авторизации и подключаюсь к WebSocket-серверу		
	Состояние = ВебсокетКлиент.ПолучитьСостояниеСоединения(); 	
	Если Состояние = СостояниеWebSocketСоединения.Закрыто Тогда	
		
		// Авторизация с ручным добавлением заголовка
		ЛогинПарольБасе64 = ПолучитьBase64СтрокуИзДвоичныхДанных(
			ПолучитьДвоичныеДанныеИзСтроки(Логин()+":"+Пароль(), КодировкаТекста.UTF8));  
			
		ДанныеЗаголовка = СтрШаблон("Basic %1", ЛогинПарольБасе64);	
			
		ВебсокетКлиент.ПараметрыСоединения.Заголовки["Authorization"] = ДанныеЗаголовка;	
		ВебсокетКлиент.Подключить(); 
			
		Соединение = ВебсокетКлиент.ПолучитьТекущееСоединение();  
		КлючКлиента = Соединение.Ключ; 
		
	КонецЕсли; 
		
	Если Удалять Тогда
					
		// Проверяю открыто соединение и если открыто, отключаю 
		Состояние = ВебсокетКлиент.ПолучитьСостояниеСоединения();   
		Пока Состояние <> СостояниеWebSocketСоединения.Открыто Цикл 
		    Состояние = ВебсокетКлиент.ПолучитьСостояниеСоединения();
		КонецЦикла;		
		ВебсокетКлиент.Отключить(); 
        
		ВебсокетКлиент.Удалить();  
		
		КлючКлиента = "";
		
	КонецЕсли; 
			
КонецПроцедуры
&НаСервереБезКонтекста
Функция Логин()
	Возврат "myUser";
КонецФункции
&НаСервереБезКонтекста
Функция Пароль()
	Возврат "123";
КонецФункции
&НаСервереБезКонтекста
Функция КлючСоединения() 		
	Возврат Строка(Новый УникальныйИдентификатор);
КонецФункции

&НаКлиенте
Процедура Отправить(Команда) 
	
	Если ЗначениеЗаполнено(КлючКлиента) Тогда 
		ОтправитьНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтправитьНаСервере() 
		
	Соединение = WebSocketКлиентСоединения.ПолучитьСоединение(КлючКлиента);
	Если Соединение <> Неопределено Тогда  
		
		// Отправляем текст
		Соединение.ОтправитьСообщение(Сообщение); 
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьДД(Команда)
	
	Если ЗначениеЗаполнено(КлючКлиента) Тогда
		ОтправитьДДНаСервере();
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ОтправитьДДНаСервере()
	
	Соединение = WebSocketКлиентСоединения.ПолучитьСоединение(КлючКлиента);
	Если Соединение <> Неопределено Тогда  
		
		// Отправляем двоичные данные
		НовыйПоток = Новый ПотокВПамяти; 
		ЗаписьТекста = Новый ЗаписьТекста(НовыйПоток);
		ЗаписьТекста.Записать(Сообщение);  
		ЗаписьТекста.Закрыть();
		ДвоичныеДанные = НовыйПоток.ЗакрытьИПолучитьДвоичныеДанные(); 
		
		Соединение.ОтправитьСообщение(ДвоичныеДанные);
		
	КонецЕсли;

КонецПроцедуры
